trait IQue[RSP extends IQueRsp] extends PseudoEntity with IVisabilityOwner {
  func n: UInt = version.questions.indexOf(this)!
  impl func title = "$version $number"
  ref compute number: String = n + "."
  impl field version: QrVersion
  impl func mainEntity = version
  
  func optional: Bool
  
  ref field comments: Text?
  
  func question: String?
  func needVersion = false
  
  func makeRsp(qr: IQr): IQueRsp
  func response(qr: IQr): RSP = qr.responses.find(_.que == this)!.as[RSP]
  func clone(v: QrVersion) = {
    let r = this.copy(version = v, visibility = [])
    r.visibility = visibility*.clone(r)
    r
  }
  
  
  view footer = y {
    comments >> field(visible = ui.isSelected)
    visibility >> field(visible = visibility || ui.isSelected, frame = false, label = "")
  }
}

trait IQueField[RSP extends IQueRsp] extends IQue[RSP] {
  impl ref field question: String? {
    validate = validateQuestion
  }
  impl ref field optional: Bool
  func validateQuestion = if (!question) "Question is requied"
 
  impl func needVersion {
    default value = !optional && updated(optional)
  }
  
  view header = x{number >> field(fontStyle = Label) question >> field(stretch = 1) optional >> field(visible = optional || ui.edit)}
}

trait IQueRsp extends InlineEntity {
  system field qr: IQr
  func que: IQue
  ref compute number: String = qr.queNumPrefix + que.number
  func inProgress: Bool = responded
  func responded: Bool
  func qrParams = qr.qrParams
  
  compute question: String? = {
    if (let prs = qrParams) {
      var s = que.question
      prs.foreach{pp =>
        s = s.replace("\$" + pp.#0.name, pp.#1)!
      }
      s
    } else {
      que.question
    }
  }
  view header = y {
    x{number >> field(fontStyle = Label) question >> field(fontStyle = qr.fontStyle)}
    que.comments >> field(fontSize = 0.8, color = "#666")
  }
  
  func isVisible = que.visibility.all(_.isVisible(this))
}