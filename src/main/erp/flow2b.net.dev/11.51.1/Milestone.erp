enum MilestoneStatus(active: Bool, rankCount: Bool, rank: UDec[2]?, going: Bool) {
  case Entered(active = false, rankCount = true, rank = 1.00, going = false) {background = "#ed9"}
  case InProgress(active = true, rankCount = true, rank = 8.00, going = true) {background = "#8bf"}
  case Implemented(active = true, rankCount = true, rank = 10.00, going = true) {background = "#dea"}
  case Verified(active = true, rankCount = true, rank = 10.00, going = true) {background = "#dea"}
  case Production(active = false, rankCount = false, rank = 20.00, going = false) {
    color = "white"
    background = "#090"
  }
  case Closed(active = false, rankCount = false, rank = 0.00, going = false) {
    background = "#999"
  }
}
 
entity Milestone extends Entity with Blockable {
  background = "#545790"
  impl func title: String = if (let p = products.onlyOne) p.title + ": " + name else name
  ref field name: String
  ref field status: MilestoneStatus = Entered
  
  impl func itemRoles = [ProductOwner]
  //impl func activeRoles = if (status.active) [ProductOwner]
  
  impl func canRead = products.exists(canRead)
  override func isBlocking(issue: Issue): Bool = issue.status.blocking || issue.status == Revision
  
  impl ref field products: [DevProduct] by milestones
  field releaseDate: Date?
  func remainingEffort: Int? = todo.map(effort?.value).sum
  impl func contentIssues: [Issue]? = milestoneIssues
  func wholeEffort: Int? = remainingEffort + milestoneIssues.filter(status == Verified).map(effort?.value).sum
  compute effort = remainingEffort + " of " + wholeEffort
  func currentNumber(product: DevProduct): UInt = (product.milestones.filter(status.rankCount).indexOf(this) + 1).max(1)
  field description: Text?
  impl ref stored compute rank: UDec[2]? = status.rank * products.map(p => p.rank/currentNumber(p)).sum
  func allDesireRank: UDec[2]? = rank//*(1.00 - 1.00/status.rank)
  impl func active: Bool = status.active || status == Entered
  impl func isInBacklog: Bool = status.active  || status == Entered
  compute todo: [Issue]? {
    value = milestoneIssues.filter(status.mlActive)
    update = {
      this.milestoneIssues = new + this.milestoneIssues.filter(!_.status.mlActive)
    }
    init = {
      new._product = products.first
      new.milestone = _
    }
  }
  compute otherIssues: [Issue]? {
    value = milestoneIssues.filter(!_.status.mlActive)
    canAdd = false
  }
  field milestoneIssues: [Issue]? read by milestone {
  	views = []
  }
  impl event onSave = {
    //milestoneIssues*.touch
    blockedByIssues*.touch
  }
  
  impl view form = nav {
    grid {
      name
      rank
      x(label = "Status"){blockStatus status}
      products
      description
      effort
      todo >> list(/*editMode = AlwaysRefRead, */frame = true){_}
      blockedByActiveIssues >> list(frame = true, label = "Blocked by Issues", singular = "Issue"){
        _
      }
      otherIssues >> list(editMode = AlwaysRefRead, frame = true){_}
    }
  }
  
  report Report {
    field status: [MilestoneStatus]? {auto = true}
    impl view list = Milestone.all >> nav {list{
       title
       x{blockStatus status >> field(visible = status != Entered) }
    }}
  }
}
