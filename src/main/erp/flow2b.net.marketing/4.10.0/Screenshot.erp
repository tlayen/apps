entity Screenshot {
  object field gen: Int?
  const ref field n: Int? = increment(Screenshot.gen)
  func number = "SCR" + n.format(3, 0)
  func title = "$number $name"
  ref field name: String
  field url: Url? {
    label = "Screenshot website URL"
    validate = if ((status == Taken|| status == Verified) && !url) "URL should be entered for taken screenshots"
  }
  field take: ScreenshotTake? {
    label = "How to take"
    validate = if ((status == Taken || status == Verified) && !take) "How to take should be entered for taken screenshots"
  }
  ref field status: ScreenshotStatus = ToTake
  field locations: [ScreenshotLocation] inline by screenshot
  func itemRoles = [Marketer]
  //func activeRoles = if (status.active) [Marketer]
  view form = nav {
    grid {
      x{
        status >> field(editMode = AlwaysEdit, compact)
        name >> field(stretch = 1, fontStyle = H2)
        number >> field(fontStyle = H3)
      }
      url
      take
      locations 
    }    
    extension("nav")
  }
  view ref = x(flow = false){status >> field(editMode = AlwaysEdit, compact = true) title}
  object view list = Screenshot.all >> nav{list{ref}}
}

entity ScreenshotPage {
  field title: String
  field url: Url
  field screenshots: [ScreenshotLocation]? inline by pages
}

inline entity ScreenshotLocation {
  field screenshot: Screenshot by locations
  field pages: [ScreenshotPage] by screenshots
  field name: String? {views = []}
  compute title {
    value = name.alt(screenshot.name)
    update = {
      if (new == screenshot.title) name = "" else name = new
    }
  }
  field description: Text?
  view form = grid(labels = None) {
    screenshot
    pages
    title
    description
  }
}

interface ScreenshotTake 

class ScreenshotExternal extends ScreenshotTake {
  singular = "Externally"
  field url: Url? {label = "Extenral URL (Optional)"}
}
class ScreenshotFlow2b extends ScreenshotTake {
  singular = "In Flow2b"
  field userName: String {
    label = "Flow2b user name to take screenshot"
  }
  field url: Url {
    label = "Flow2b URL where to take screenshots"
  }
  field steps: Text? {
    label = "Steps how to take screenshots from the URL if any"
  }
}

enum ScreenshotStatus(active: Bool) {
  case ToTake(active = true) {
    background = "#8bf"
  }
  case Taken(active = true) {
    background = "#dea"
  }
  case Verified(active = false) {
    color = "white"
    background = "#090"
  }
  case Canceled(active = false) {
    background = "#999"
  }
}