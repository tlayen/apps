entity KitPriceModel extends IDynamicPriceModel with IProdFilterOwner with IPriceModOwner {
  impl func title = "Kit Price Model"
  impl func priority: Dec[8]? = 0.5
  impl ref field start: Date = now
  impl ref field end: Date?
  
  field rules: [KitModelRule]? inline by model
  field rounding: Rounding? = Floor()
  field discount: Percent[2]? {
    validate = if (discount > 1.0000) "Discount cannot be more than 100%"
  }
  impl func price(level: IPriceLevel, type: PriceType, product: IBaseProduct): IPrice? = {
    if (let kit = product.as[Kit]){
      let rule = rules.find(filterProd(kit))
      if (rule || !rules) {
        let prices = kit.products.toSeq.map {i =>
          if (let p = vendor.price(level, type, currency, i.#0)) {
            KitItemPrice(prod = product, price = p, qty = i.#1)
          }
        }
        if (prices.all(_)) {
          KitPrice(model = this, rule = rule, level = level, type = type, kit = kit, prices = prices.map(_!))
        }
      }
    }
  }
  impl view form = nav {grid{
    row {start end}
    row {vendor, currency}
    levels
    rounding    
    rules
  }}
}

entity KitModelRule extends InlineEntity with IProdFilterOwner with IPriceModOwner  {
  field model: KitPriceModel
  impl view form = grid {
    filters >> field(showEmpty)
    mods >> field(showEmpty)
  }
}
 
class KitPrice extends IPriceSalesRent with IPriceRentDecorator {
  impl field model: KitPriceModel
  field kit: Kit
  field prices: [KitItemPrice]
  field type: PriceType
  field level: IPriceLevel
  field rule: KitModelRule? inline
  impl func levels = [level]
  impl func priceRent = prices*.price*.as[IPriceRent].first!
  
  impl func priceType: PriceType = type
  impl func product = kit
  impl func salesPrice(request: PriceRequest): Future[SalesPrice] = {
    amount(request, PriceOrderItem(prod = kit, qty = 1))
  } 
  
  impl func amount(request: PriceRequest, item: PriceOrderItem): Future[SalesPrice] = {
    let ps <- prices.oneByOne{p => p.price.amount(request, PriceOrderItem(prod = p.prod, qty = p.qty*item.qty, rentPeriod = item.rentPeriod))}
    let basePrice = SalesPrice.sum(ps)
    if (let rule) rule.applyMods(model.rounding, request, item, basePrice)
    else basePrice
  }
  
  impl func rentPrices(request: PriceRequest): Future[[DurPrice]?] = {
    let pss <- prices.filter(price.is[IPriceRent])!.oneByOne{p => 
      let rentPrices <- p.price.as[IPriceRent]!.rentPrices(request)
      (price = p, rentPrices)
    }
    let pss1 = pss.first.rentPrices.flatMap {ps =>
      if (pss.all(_.rentPrices.exists(_.dur == ps.dur))) {
        let sps: [SalesPrice] = pss.map(_.rentPrices.find(_.dur == ps.dur)!.price * _.price.qty)
        let p: SalesPrice = SalesPrice.sum(sps)
        [DurPrice(
          unit = ps.unit, dur = ps.dur
          price = p
        )]
      }
    }
    if (let rule) rule.applyMods(model.rounding, request, kit, pss1)
    else pss1
  }
  override func has(type: PriceType): Bool = type == this.type
} 

class KitItemPrice {
  field prod: IBaseProduct
  field price: IPrice {
    available(Inline)
  }
  field qty: UQty
}

