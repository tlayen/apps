extend trait IIncomeType extends HasExtId
extend entity SalaryType extends HasExtId

back class XeroCache {
  field service: XeroIntegration
  field _accounts: [XeroAccount]?
  func accounts: Future[[XeroAccount]] = if (let _accounts) _accounts
  else {
    let accRes <- service.https("Accounts").get
    let accounts = accRes.body!.parseJson[XeroAccountsResponse].accounts
    _accounts = accounts
    accounts
  }
  func account(code: String): Future[XeroAccount] = {
    let accs <- accounts
    accs.find(a => a.code =%= code).alt(throw "XeroCache: can't find xero account with code = $code")
  }
}

extend entity XeroIntegration {
  back func expenseType(cache: XeroCache, accCode: String?): Future[ExpenseType] = {
    if (let accCode) {
      if (let et = findExtCode(accCode, _.as[ExpenseType])) {
        et
      } else {
        let liAcc <- cache.account(accCode)
        let et: ExpenseType = ExpenseType.all.find(title == liAcc.name).result.get
          .alt(ExpenseType(title = liAcc.name!, description = liAcc.description))
        if (let tt = liAcc.taxType) {
          if (let gstRate = GstRate.all.find(g => g.rate == tt?.rate)) {
            et.gstRate = gstRate
          } else {
            et.gstRate = Special
            et.specialGstRate =? tt?.rate
          }
        }
        updateExtId(et, liAcc.accountID)
        updateExtCode(et, accCode)
        et
      }
    } else {
      ExpenseType.all.find(title == "Purchases").result.get.alt(ExpenseType(title = "Purchases").save)
    }
  } 
  
  back func revenueType(cache: XeroCache, accCode: String?): Future[RevenueType] = {
    if (let accCode) {
      if (let et = findExtCode(accCode, _.as[RevenueType])) {
        et
      } else {
        let liAcc <- cache.account(accCode)
        let et: RevenueType = RevenueType.all.find(title == liAcc.name).result.get
          .alt(RevenueType(title = liAcc.name!, description = liAcc.description))
        if (let tt = liAcc.taxType) {
          if (let gstRate = GstRate.all.find(g => g.rate == tt?.rate)) {
            et.gstRate = gstRate
          } else {
            et.gstRate = Special
            et.specialGstRate =? tt?.rate
          }
        }
        updateExtId(et, liAcc.accountID)
        updateExtCode(et, accCode)
        et
      }
    } else {
      RevenueType.all.find(title == "Sales").result.get.alt(RevenueType(title = "Sales").save)
    }
  } 
  
  back func revenueTypeNoSales(cache: XeroCache, accCode: String): Future[RevenueType?] = {
    if (let et = findExtCode(accCode, _.as[RevenueType])) {
      et
    } else {
      let liAcc <- cache.account(accCode)
      if (let et = RevenueType.all.find(title == liAcc.name).result.get
        .or(if (liAcc.name != "Sales") RevenueType(title = liAcc.name!, description = liAcc.description)))
      {    
        updateExtId(et, liAcc.accountID)
        updateExtCode(et, accCode)
        et
      }
    }
  } 
  
  back func salaryType(cache: XeroCache, accCode: String): Future[SalaryType] = {
    if (let et = findExtCode(accCode, _.as[SalaryType])) {
      et
    } else {
      let liAcc <- cache.account(accCode)
      let et: SalaryType = SalaryType.all.find(title == liAcc.name).result.get
        .alt(SalaryType(title = liAcc.name!, description = liAcc.description))
      updateExtId(et, liAcc.accountID)
      updateExtCode(et, accCode)
      et
    }
  } 
  
  back func finAccount(cache: XeroCache, acc: XeroAccount): Future[FinAccount] = {
    if (let et = findExtId(acc.accountID, _.as[FinAccount])) {
      et
    } else {
      let liAcc <- cache.account(acc.code!)
      let et: FinAccount = FinAccount.all.find(title == acc.name).result.get
        .alt(CashAccount(legalEntity = cache.service.legalEntity, name = liAcc.name!, currency = cache.service.legalEntity.getCurrency))
      updateExtId(et, liAcc.accountID)
      updateExtCode(et, liAcc.code)
      et
    }
  } 
}