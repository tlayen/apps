class RetrieveInvoicesJob extends SystemJob {
  impl field service: XeroIntegration
  field pageNumber: UInt = 1
  field accInvoices: [XeroInvoice]? = []
  
  impl field errorAction = IncrementCounter

  impl func do = {
    let res <- service.https("Invoices")
    			.param("Statuses", "PAID,AUTHORISED,SUBMITTED,DRAFT")
				.param("page", pageNumber)
                .param("unitdp", 4)
    			.get
    let rb = res.body!.parseJson[XeroInvoicesResponse]
    
    let dateFrom = service.setup.pullInvoicesFrom
    let invs = rb.invoices.filter{ xi => 
      !dateFrom || (xi.maxDate >= dateFrom) || (xi.status != paid)
    }
    let allInvs = accInvoices + invs
    
    if (rb.invoices.count > 0) {
      RetrieveInvoicesJob(
        service = this.service,
        pageNumber = this.pageNumber + 1,
        accInvoices = allInvs,
        errorAction = this.errorAction
      ).enqueueFirst()  // it is important to enqueue *First* because InitialSyncFinishedJob
    } else {
      allInvs.foreach{inv => 
        RetrieveInvoiceJob(service = this.service, invoice = inv, errorAction = this.errorAction).enqueueFirst() // it is important to enqueue *First* because InitialSyncFinishedJob
      }
    }
  }
}


back class RetrieveInvoiceJob extends SystemJob {
  impl field service: XeroIntegration
  field invoice: XeroInvoice
  
  impl field errorAction = IncrementCounter
  
  impl func toString = "RetrieveInvoiceJob $(invoice.invoiceNumber) $(invoice.invoiceID)"
  
  impl func do = {   
    if (invoice.status != voided &&
        (invoice.status != draft || service.setup.pullDraftInvoices)) {
      // invoice
      let order <- saveInvoice(invoice)

      // credit notes
      let _ <- invoice.creditNotes*.creditNoteID.oneByOne { cnID =>
        RetrieveCreditNoteJob(service = this.service, creditNoteId = cnID).do
      }

      // payments
      let _ <- if (order.created && !invoice.payments.isEmpty) {
        RetrieveInvoicePaymentsJob(service = this.service, invoiceID = invoice.invoiceID!).do()
      }
      
      /*let _ <- if (order.created) {
        invoice.payments*.paymentID.oneByOne { pID =>
          RetrievePaymentJob(service = this.service, paymentID = pID, force = true).do
        }
      }*/

      order
    } 
  }
  
  
  back func saveInvoice(xi: XeroInvoice): Future[Order] = {
    let cache = XeroCache(service = service)
    if(let o = service.findExtId(xi.invoiceID, _.as[Order])) {      
      o
    } else {
      let invCounterparty: LegalEntity <- service.getOrRetrieveContact(xi.contact!.contactID!)

      if((xi.type == accRec) && !invCounterparty.vendor) {
        invCounterparty.makeVendor()
        invCounterparty.vendor!.gstStatus = Registered
      }
      // invCounterparty.log
      // itms.map(item.itemID, product.title)*.log

      let ord: Order = if(xi.type == accRec) SalesOrder(
        refNum = xi.invoiceNumber
        customer = invCounterparty.makeCustomer
        vendor = service.legalEntity
      ) else PurchaseOrder(
        refNum = xi.invoiceNumber
        customer = service.legalEntity
        vendor = invCounterparty.makeVendor
      )

      ord.date = xi.date.alt(Date.now)
      ord.accepted = if(xi.status != draft) ord.date
      if(xi.type == accRec){
        if (let d = ord.deliveries.first.as[SalesDelivery]){
          d.store = service.store
          if (xi.status == paid) {
            d.state = Complete
            d.start = ord.date
          }
        }
      } else {
        if(let d = ord.deliveries.first.as[PurchaseDelivery]){
          d.store = service.store
          if (xi.status == paid) {
            d.state = Complete
            d.start = ord.date
          }
        }
      }
           
      ord.save()
      
      let itms = xi.lineItems //.filter(li => li.itemCode || li.accountCode) this filter is not needed more, 
      						  // because we can create RevenueItem/ExpenseItem for items by accountCode or even without it.
      
      let _ <- if(xi.type == accRec) {
        itms.oneByOne { li =>
          let si: OrderItem <- {
            if (li.unitAmount < 0) {
              SalesDiscountItem(
                order = ord.as[SalesOrder]!
                gstRate = li.taxType?.rate,
                deliveryItem = DiscountDeliveryItem()
              )
            } else {
              let prod: ISubProduct? <- prodByCode(cache, li.itemCode)
              if (let p = prod) {
                if (p.status == NotForSale && p.rentStatus == ForRent) { 
                  let sri = SalesRentItem (
                    order = ord.as[SalesOrder]!
                    deliveryItem = RentDeliveryItem(
                      product = p
                      dur = (if (service.setup.invoiceQtyMeaning == Duration) li.quantity.positive).alt(1)
                      qty = (if (service.setup.invoiceQtyMeaning == Qty) li.quantity.positive).alt(1)
                    ) 
                    itemDesc = if (p.title != li.description) {let d: Text? = li.description
                                                              d}
                    gstRate = li.taxType?.rate
                  )
                  let pp <- sri.productPrice
                  sri.deliveryItem.durUnit =? pp?.rentDurUnits.first
                  sri.delivery.end = sri.delivery.start
                  sri.save
                } else {
                  if (li.quantity.as[Int] != li.quantity && p.unit != Part) {
                    makePart(p)
                  }
                  SalesItem (
                    order = ord.as[SalesOrder]!
                    deliveryItem = DeliveryItem( 
                      product = p
                      unit = p.unit
                      qty = li.quantity.positive.alt(1)
                    )
                    itemDesc = if(p.title != li.description) {let d: Text? = li.description
                                                              d}
                    gstRate = li.taxType?.rate
                  ).save
                }
              } else {
                let ex <- service.revenueType(cache, li.accountCode)
                SalesItem (
                  order = ord.as[SalesOrder]!
                  deliveryItem = DeliveryItem( 
                    product = ex
                    unit = ex.unit
                    qty = li.quantity.positive.alt(1)
                  )
                  itemDesc = if(ex.title != li.description) {let d: Text? = li.description
                                                         d}
                  gstRate = li.taxType?.rate
                ).save
              }
            }
          }
          
          let am: Money[2]? = li.unitAmount * li.quantity
          let disc = ((am*li.discountRate).round(halfDown, 0).as[Money[4]]/100)
          if (let am = am.positive) {
            if (xi.lineAmountTypes == Inclusive) {
              si.amountWithGst = am
              si.discountWithGst = disc.positive
            } else {
              si.amountNoGst = am
              si.discountNoGst = disc.positive
            }
          } else {
            if (xi.lineAmountTypes == Inclusive) {
              si.discountWithGst = (disc - am).positive
            } else {
              si.discountNoGst = (disc - am).positive
            }
          }
          
          si.save
        } 
      } else {
        itms.oneByOne { li =>
          let pi: OrderItem <- {
            if (li.unitAmount < 0) {
              PurchaseDiscountItem(
                order = ord.as[PurchaseOrder]!
                gstRate = li.taxType?.rate,
                deliveryItem = DiscountDeliveryItem()
              )
            } else {
              let prod: ISubProduct? <- prodByCode(cache, li.itemCode)
              if (let p = prod) {
                if (li.quantity.as[Int] != li.quantity && p.unit != Part) {
                  makePart(p)
                }
                PurchaseItem (
                  order = ord.as[PurchaseOrder]!
                  deliveryItem = DeliveryItem( 
                    product = p
                    unit = p.unit
                    qty = li.quantity.positive.alt(1)
                  )
                  itemDesc = if(p.title != li.description) {let d: Text? = li.description
                                                               d}
                  gstRate = li.taxType?.rate
                ).save             
              } else {
                let ex <- service.expenseType(cache, li.accountCode)
                PurchaseItem (
                  order = ord.as[PurchaseOrder]!
                  deliveryItem = DeliveryItem( 
                    product = ex
                    unit = ex.unit
                    qty = li.quantity.positive.alt(1)
                  )
                  itemDesc = if(ex.title != li.description) {let d: Text? = li.description
                                                         d}
                  gstRate = li.taxType?.rate
                ).save
              }
            }
          }
          let am = li.unitAmount * li.quantity
          let disc = ((am*li.discountRate).round(halfDown, 0).as[Money[4]]/100)
          if (let am = am.positive) {
            if (xi.lineAmountTypes == Inclusive) {
              pi.amountWithGst = am
              pi.discountWithGst = disc.positive
            } else {
              pi.amountNoGst = am
              pi.discountNoGst = disc.positive
            }
          } else {
            if (xi.lineAmountTypes == Inclusive) {
              pi.discountWithGst = (disc - am).positive
            } else {
              pi.discountNoGst = (disc - am).positive
            }
          }
          
          pi.save
        }
      }
      
      ord.updateExtId(service, xi.invoiceID)
      
      let linkPayRec = if (xi.type == accRec) "AccountsReceivable" else "AccountsPayable" 
      ord.updateExtUrl(service, "https://go.xero.com/$linkPayRec/View.aspx?InvoiceID=${xi.invoiceID}")
      
      if(xi.status != draft) {
        let inv = ord.createInvoice(xi.date.alt(Date.now))
        inv.date = xi.date.alt(Date.now)
        inv.dueDate = xi.dueDate
        inv.totalDue = ord.totalWithGst
        inv.save()
      }

      ord.saveMain
    }
  }
  private back func makePart(p: ISubProduct): Void = {
     if (let pp = p.superProduct.as[ISuperProduct with IAltUnits]) {
       pp.unit = Part
       pp.save
     } else if (let pp = p.superProduct.as[Service]) {
       pp.unit = Part
       pp.save
     } else {
       throw "Cannot update unit for product $p"
     }
     DeliveryItem.all.filter(product == p).update(unit = Part)
  }
  
  private back func prodByCode(cache: XeroCache, code: String?): Future[ISubProduct?] = {
    if (let code) {
      if (let prod = service.findExtCode(code, _.as[ISubProduct])) {
        Future(prod)
      } else {
        {
          let ires <- service.https("Items/$code").get
          let itm = ires.body!.parseJson[XeroItemsResponse].items.first
          
          if(service.setup.pullItems) {
            let pr <- RetrieveItemsJob(service = this.service).saveItem(cache, itm)
            Option.some(pr)
          } else {
          	RetrieveMatchedItemsJob(service = this.service).matchItem(itm)
          }
        }.recover{err =>
          if (err.as[HttpResponse]?.code == 404) {
            let p : ISubProduct? = null
            let pp: ISubProduct?? = Option.some[ISubProduct?](p)
            pp
          } else {
            null
          }
        }
      }
    } else {
      Future(null[ISubProduct])
    }
  }
}