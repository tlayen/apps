class RetrieveAssetsJob extends SystemJob {
  impl field service: XeroIntegration
  field pageNumber: UInt = 1
  field accAssetIds: [String]? = []
  
  impl field errorAction = IncrementCounter
  
  impl func do = {
    let res <- service.assetsHttps("Assets").param("status", "REGISTERED").param("page", pageNumber).get // status=Registered
    let rb = res.body!.parseJson[XeroAssetsResponse]

    let allAssetIds = accAssetIds + rb.items*.assetId

    if(rb.pagination.pageCount > pageNumber) {
      RetrieveAssetsJob(
        service = this.service,
        pageNumber = this.pageNumber + 1,
        accAssetIds = allAssetIds,
        errorAction = this.errorAction
      ).enqueueFirst() // it is important to enqueue *First* because InitialSyncFinishedJob
    } else {
      allAssetIds.map { aId =>
        RetrieveOneAssetJob(service = this.service, assetId = aId, errorAction = this.errorAction).enqueueFirst() // it is important to enqueue *First* because InitialSyncFinishedJob
      }
    }
  } 
}


class RetrieveOneAssetJob extends SystemJob {
  impl field service: XeroIntegration
  field assetId: String
  
  impl field errorAction = IncrementCounter
  
  impl func do = {
    let res <- service.assetsHttps("Assets/$assetId").get
    let xeroAsset = res.body!.parseJson[XeroAsset]
    //res.body.log("asset body")
    saveAsset(xeroAsset)
  }
  
  back func saveAsset(xeroAsset: XeroAsset) = {
    //xeroAsset.json.log 
    
    if(!service.findExtId(xeroAsset.assetId, _.as[ProductItem])) {
      let existingGood = Good.all.find(title == xeroAsset.assetName && status == NotForSale).result.get
      let product = existingGood.alt(Good(
        title = xeroAsset.assetName! 
//        refNum = xeroAsset.assetNumber
        status = NotForSale
      ))

      if(let ds = xeroAsset.bookDepreciationSettings) {
        if(ds.depreciationMethod == StraightLine) {
          product.assetType = AssetType(
            title = ds.depreciationMethod!.id! + ": " + ds.depreciationRate!,
            depreciation = StraightLine(
              rate = ds.depreciationRate!
            )
          ).saveBy(title)
        }else if([XeroDepreciationMethod.DiminishingValue100,
                  XeroDepreciationMethod.DiminishingValue150,
                  XeroDepreciationMethod.DiminishingValue200]
                 .contains(ds.depreciationMethod!)){
          product.assetType = AssetType(
            title = ds.depreciationMethod!.id! + ": " + ds.depreciationRate!,
            depreciation = DiminishingValue(
              rate = ds.depreciationRate!
            )
          ).saveBy(title)
        } 
      }
      
      if(xeroAsset.serialNumber) product.trackItems = Sometimes
      
      product.save
      
      let productItem = ProductItem(
        product = product
        serialNum = xeroAsset.serialNumber
        refNum = xeroAsset.assetNumber
      )
      productItem.save
      productItem.updateExtId(service, xeroAsset.assetId)
      
      
      let writeUp = InventoryWriteUp(
        product = product
        legalEntity = service.legalEntity
        store = service.store
        stockType = Asset
        productItems = [productItem] // ???
        unit = product.unit
        date = xeroAsset.bookDepreciationDetail?.depreciationStartDate.alt(xeroAsset.purchaseDate!)
        price = xeroAsset.purchasePrice.positive // ???
      ) 
      writeUp.save   
    }
  }
}
