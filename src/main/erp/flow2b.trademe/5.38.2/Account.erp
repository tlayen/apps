back class TrademeAccount extends ECustomer[Int] {
  extFieldNaming = UpperCamelCase
  field memberId: Int
  field nickname: String
  func title = nickname
  field isAddressVerified: Bool
  field dateAddressVerified: Date? {extMap = TrademeOptDate}
  field dateJoined: Date? {extMap = TrademeOptDate}
  field email: Email?
  field uniqueNegative: Int? {label = "Negative Feedbacks"}
  field uniquePositive: Int? {label = "Positive Feedbacks"}
  field feedbackCount: Int?
  field isDealer: Bool
  
  impl func personNickname = nickname
  impl func extId = memberId
  impl func emails = email.toSeq
  impl func phones = []
  impl func billingAddresses = []
  impl func deliveryAddresses = []
  impl func personFullName = ""
  
  impl func update(customer: Customer): Void = {
    TrademeAccountContact.add(customer.legalEntity, nickname)
  }
  
  override func fromContact(customer: Customer): Contact? = {
    customer.legalEntity.contacts*.as[TrademeAccountContact].first
  }
}

entity TrademeAccountContact extends ActorContact {
  singular = "Trademe Account"
  plural = "Trademe Account"
  ref field nickname: String
  impl func address: String = nickname
  impl view form = x{
    h3("Trademe Account")
    nickname
  }
  back object func add(addressee: Addressee, nickname: String): TrademeAccountContact = {
    addressee.contacts*.as[TrademeAccountContact].find(a => a.nickname == nickname)
      .alt(TrademeAccountContact(addressee = addressee, nickname = nickname).save)
  }
}

back class TrademeLocation extends EAddress {
  extFieldNaming = UpperCamelCase
  field name: String? 
  field address1: String?
  field address2: String?
  field suburb: String?
  field city: String?
  field postcode: String?
  field country: String?
  field phoneNumber: Phone?
  impl func address = if (let street = "$address1\n$address2".trim) Address(
    person = name, street = street
    suburb = suburb, city = city, postcode = postcode, country = country
  )

  impl func phones = phoneNumber.toSeq
  impl func emails = []
}
