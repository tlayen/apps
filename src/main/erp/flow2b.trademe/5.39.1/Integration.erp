trait ITrademeIntegration extends SystemService with OAuth1Hmac with ExtIdSource[Int] with ISalesChannel with SalesOrderUpdater[Int] {
  impl field title: String = "Trade Me"
  impl func roles: [Role]? = [Admin]
  impl func authParams = ["scope" -> "MyTradeMeRead,MyTradeMeWrite"]
  
  const field initComplete: Bool

  field initialDownload: TrademeInitialDownload? inline by service {
    update = {
      if (!old && new) {
        manageListings = false
      }
    }
  }
  func tmExtUrl: Url
  func webUrl: Url
  func https(path: Url): HttpRequest = HttpRequest(url = "$tmExtUrl$path", ssl = true).oauth(this)
  
  field manageListings: Bool = true
  field templates: [TrademeTemplate]? read by integration {deleteCascade = true}
  system field retrieveSoldItemsJob: RetrieveSoldItemsJob inline by service
  system field retrieveQuestionsJob: RetrieveQuestionsJob inline by service
  ref field legalEntity: OurLegalEntity {
    available(Ref).with(priceModels)
    default =? onlyOne
  }
  field approvedDealsSeller: Bool
  
  field finAccount: TrademeFinAccount inline by integration {
    label = "Member Account"
  }
  field pingAccount: PingAccount inline by integration
  field payNowAccount: PayNowAccount inline by integration
  field trademe: Vendor {
    label = "Trademe Company"
    default <- {
      let comp <- Company.all.find(title.contains("Trade Me") || title.contains("Trademe")).result
      let v = comp.alt(
        Company(
          title = "Trade Me", currency = NZD, legalInfo = LegalInfoNZ(name = "Trade Me Limited"),
          altNames = ["Trademe"]
        )
      ).makeVendor
      v.gstStatus = Registered
      v
    }
  }
  impl func uploadOrderState(order: SalesOrder): Void = {
    PushSalesOrderState(service = this, so = order).enqueue
  }
  impl back func error(job: ISystemJob, error: Any): JobError = {
    if (let e = error.as[JobError]) e
    else if(error.as[HttpResponse]?.code == 503 || error.as[HttpResponse]?.code == 500) {
      touch
      saveMain
      let date = Date.now + 1_hour
      Activity.comment("Server unavailable, will try again at $date")
      RepeatJob(times = 4, on = date, reason = error)
    } else if(error.as[HttpResponse]?.code == 429) {
      touch
      saveMain
      let date = Date.now.start(of = hour) + 1_hour + 1_minute
      Activity.comment("API limit is reached, will try again at $date")
      RepeatJob(times = 1, on = date, reason = error)
    } else {
      ReportError(error = error)
    }
  }

  
  impl view form = nav{
    tabs {
      grid(label = "Main") {
        issuesView
        title
        legalEntity
        serviceState
        manageListings
        authorizeView
        approvedDealsSeller
        trademe
        templates
        initialDownload
      }
      grid(label = "Member Account") {
        finAccount.form.unnav
      }
      grid(label = "Ping") {
        pingAccount.form.unnav
      }
      grid(label = "Pay Now") {
        payNowAccount.form.unnav
      }
    }
    button("↻ Sales") {
      retrieveSoldItemsJob.enqueue()
      UI.info("Job is posted")
    }
    button("↻ Questions") {
      retrieveQuestionsJob.enqueue()
      UI.info("Job is posted")
    }
    button("↻ Member Account") {
      finAccount.enqueue()
      UI.info("Job is posted")
    }
    button("↻ Pay Now") {
      payNowAccount.enqueue()
      UI.info("Job is posted")
    }
  }
  view test = nav{
    grid{
      button("Update Categories") {
        UpdateCategoriesJob(service = this).enqueue()
        UI.info("Job is posted")
      }
      button("Retrieve Latest Sales") {
        retrieveSoldItemsJob.enqueue()
        UI.info("Job is posted")
      }  
      button("Retrieve Questions") {
        retrieveQuestionsJob.enqueue()
        UI.info("Job is posted")
      }
      button("Download Listings") {
        DownloadListingsJob(service = this).enqueue
        UI.info("Job is posted")
      }
      button("Update All Listings") {
        templates*.enqueue()
        UI.info("Job is posted")
      }
    }
  }
  
  impl event onSave = {
    if (authorized && updated(accessToken)) {
      UpdateCategoriesJob(service = this).enqueue()
    }
    if (serviceState == Active && initialDownload && !initComplete) {
      DownloadListingsJob(service = this).enqueue
    }
    if (manageListings && serviceState == Active && updated(serviceState)) {
      retrieveSoldItemsJob.enqueue()
      retrieveQuestionsJob.enqueue()
      TrademeQuestion.all.filter(status != Answered && answer).foreach {q =>
        PostAnswerJob(question = q).enqueue()
      }
      finAccount.enqueue()
      templates*.enqueue()
    }
  }
}

inline entity TrademeInitialDownload {
  field service: ITrademeIntegration by initialDownload
  field products: Bool = true
  field photos: Bool = true
  field template: TrademeTemplate
  field stocktakeStore: IStore =? IStore.onlyOne
  impl view form = grid{
    row {products photos}
    template
    stocktakeStore
  }  
}

entity TrademeIntegration extends ITrademeIntegration {
  impl func authTmpTokenUrl: Url = "https://secure.trademe.co.nz/Oauth/RequestToken"
  impl func authAuthorizeUrl: Url = "https://secure.trademe.co.nz/Oauth/Authorize"
  impl func authTokenExchangeUrl: Url = "https://secure.trademe.co.nz/Oauth/AccessToken"
    
  impl func authConsumerKey: String = "A4FA0ACAFE93D0DD7AF5BA7D04D5395F"
  impl func authConsumerSecret: String = "F8E7FC4A2164F73BBAEB4435A26B5522"
    
  impl func tmExtUrl = "api.trademe.co.nz/v1/"
  impl func webUrl: Url = "http://www.trademe.co.nz/"
}
