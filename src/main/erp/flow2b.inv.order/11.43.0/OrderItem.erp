inline trait OrderItem extends IAmountItem with IOrderItem {
  impl var order: Order
  field desc: Text? {label = "Description"}
  //func inTransit: Bool = itemDelivery?.status == InTransit
  func paid: Bool = order.paid
  func priceLevel: IPriceLevel = order.priceLevel

  //func complete = delivered && paid
  func priceOrderItem: PriceOrderItem? = null
  func priceRequest = order.priceRequest
  
  impl func currency = order.currency 
  impl func pricesIncludingGst = order.pricesIncludingGst 
  func orderAmount = if (pricesIncludingGst) orderAmountWithGst else orderAmountNoGst
  func orderAmountNoGst = totalNoGst
  func orderAmountWithGst = totalWithGst
  impl func invoiceAmountNoGst = totalNoGst
  impl func invoiceAmountWithGst = totalWithGst
  func orderDlvItem: IOrderDlvItem?
  impl func itemDlv: OrderDlv?
  impl func canceled = order.canceled
  impl func needsRevision: Bool = updated(sAmount) || updated(gstRate) || updated(desc)
  impl func sameAsPrev: Bool = if(let p = prev?.as[OrderItem]) {
    p.sAmount == sAmount && p.gstRate == gstRate && p.desc == desc
  }
  func quoteSwitch(quote: Bool): Void = {}  
  impl func considerGst: Bool = order.legalEntity.hasGst
  func orderDiscount: Money? = 0
  impl event onSave = {
    if (registerUpdated) {
      order.updateRegisters
    }
  }
  func invoiceDesc = desc
  func name: String
  func fullDesc: Text = if (let d = desc) "$name\n$d" else name
  func doRepeat(o: Order, d: OrderDlv): OrderItem = this.copy(order = o, invoices = [])
}

inline trait OrderProductItem extends OrderItem with IOrderProdItem with IAmountQtyItem with IncomeRecodable {
  impl func orderDlvItem: IOrderDlvItem? = deliveryItem
  var deliveryItem: IDlvProductItem
  impl func product = deliveryItem.product
  func baseQty = deliveryItem.baseQty
  impl func priceOrderItem = PriceOrderItem(prod = product, qty = qty)
  impl func defaultGstRate = product.gstRate(order.orderVendor?.legalEntity)
  impl func qty = deliveryItem.qty
  impl func invoiceQty: UQty? = qty
  impl func hasProgress = deliveryItem.is[IDlvProgressItem]
  func unit = deliveryItem.unit
  override func needsRevision: Bool = {
    deliveryItem.needsRevision || updated(sAmount) || updated(sDiscount) || updated(_discountUnit) || updated(gstRate) || updated(desc)
  }
  override func sameAsPrev: Bool = if (let p = prev?.as[OrderProductItem]) {
    deliveryItem.sameAsPrev(p.deliveryItem) && p.sAmount == sAmount && p.sDiscount == sDiscount && p.discountUnit == discountUnit && p.gstRate == gstRate && p.desc == desc
  }
  override func invoiceDesc = itemDesc
  impl func name: String = product.title
  override func fullDesc: Text = if (let d = itemDesc) "$name\n$d" else name
  compute itemDesc: Text? {
    label = "Description"
    value = desc.or(product.invoiceDesc)
    update = {
      this.desc = if (new != product.invoiceDesc) new
    }
  }
  field nonTaxable: UPercent? {
    default = income?.nonTaxable
    validate = if (0 > taxable || taxable > 1.00_PercentUnit) "Taxable percentage should be between 0% and 100%"
  }
  impl func recode(): Void = {
    if (let income) {
      this.gstRate = if (let p = order.orderVendor) income.gstRate(p.legalEntity)
      this.nonTaxable = income.nonTaxable
      this.save
    }
  }
  impl func income = (try? product)?.as[IIncomeType]
  
  compute taxable: UPercent? {
    value = (1.00_PercentUnit - nonTaxable).as[UPercent]
    update = {
      nonTaxable = (1.00_PercentUnit - new).as[UPercent]
    }
  }
  compute accessories: [ISubProduct]? = {
    product.superProduct.crossSells*.products.filter(p => !order.items*.as[OrderProductItem]*.product*.superProduct.contains(p))*.subProducts
  }
  override func doRepeat(o: Order, d: OrderDlv) = this.copy(order = o, invoices = [], deliveryItem = deliveryItem.copy(delivery = d))
}
